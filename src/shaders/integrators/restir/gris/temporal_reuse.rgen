#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_debug_printf : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_EXT_buffer_reference2 : require
#extension GL_EXT_shader_atomic_float : require

#define SCENE_TEX_IDX 8
#include "gris_commons.glsl"
GrisDirectLighting direct_lighting_buffer = GrisDirectLighting(scene_desc.gris_direct_lighting_addr);
PrefixContributions prefix_contributions = PrefixContributions(scene_desc.prefix_contributions_addr);
layout(binding = 4, std430) readonly buffer PathReconnections { ReconnectionData reconnection_data[]; };
layout(binding = 5, std430) readonly buffer InReservoirs { Reservoir in_reservoirs[]; };
layout(binding = 6, std430) writeonly buffer OutReservoirs { Reservoir out_reservoirs[]; };
layout(binding = 7, std430) readonly buffer CurrGBuffer { GBuffer curr_gbuffers[]; };

ivec2 get_neighbor_offset(inout uvec4 seed) {
	const float randa = rand(seed) * 2 * PI;
	const float randr = sqrt(rand(seed)) * pc_ray.spatial_radius;
	return ivec2(floor(cos(randa) * randr), floor(sin(randa) * randr));
}

bool process_reservoir(inout Reservoir reservoir, inout float m_c, in Reservoir canonical_reservoir,
					   in Reservoir source_reservoir, in ReconnectionData data, uint neighbor_coords_idx,
					   float canonical_in_canonical_pdf, out vec3 curr_reservoir_contribution) {
	source_reservoir.M = min(source_reservoir.M, 20);
	// GBuffer neighbor_gbuffer = g_buffers.d[neighbor_coords_idx];
	// if (!gbuffer_data_valid(neighbor_gbuffer)) {
	// 	reservoir.M += source_reservoir.M;
	// 	m_c += 1.0;
	// 	return false;
	// }
	float neighbor_in_neighbor_pdf = calc_target_pdf(source_reservoir.data.reservoir_contribution);
	float neighbor_in_canonical_pdf = calc_target_pdf(data.reservoir_contribution) * data.jacobian;
	const float m_i_num = source_reservoir.M * neighbor_in_neighbor_pdf;
	const float m_i_denom = m_i_num + canonical_reservoir.M * neighbor_in_canonical_pdf / pc_ray.num_spatial_samples;
	float m_i = m_i_denom == 0 ? 0 : m_i_num / m_i_denom;

	if (m_i <= 0.0 || m_i >= 1.0) {
		reservoir.M += source_reservoir.M;
		m_c += 1.0;
		return false;
	}

	const float m_c_num = source_reservoir.M * data.target_pdf_in_neighbor;
	const float m_c_denom = m_c_num + canonical_reservoir.M * canonical_in_canonical_pdf / pc_ray.num_spatial_samples;
	const float m_c_val = m_c_denom == 0.0 ? 0.0 : m_c_num / m_c_denom;
	if (m_c_val <= 0.0) {
		reservoir.M += source_reservoir.M;
		m_c += 1.0;
		return false;
	}
	m_c += 1.0 - m_c_val;
	ASSERT1(m_c_val > -1e-3 && m_c_val <= 1.001, "m_c_val <= 1.0 : %f\n", m_c_val);
	ASSERT1(m_i > -1e-3 && m_i <= 1.001, "m_i <= 1.0 : %f\n", m_i);
	bool accepted = combine_reservoir(seed, reservoir, source_reservoir, neighbor_in_canonical_pdf * m_i);
	if (accepted) {
		curr_reservoir_contribution = data.reservoir_contribution;
	}
	return accepted;
}

void main() {
	GBuffer gbuffer = curr_gbuffers[pixel_idx];
	if (!gbuffer_data_valid(gbuffer)) {
		imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(0));
		return;
	}

	Reservoir reservoir;
	init_reservoir(reservoir);

	Reservoir canonical_reservoir = in_reservoirs[pixel_idx];

	const vec3 canonical_reservoir_contribution = canonical_reservoir.data.reservoir_contribution;

	uint num_spatial_samples = pc_ray.enable_spatial_reuse == 1 ? pc_ray.num_spatial_samples : 0;
	vec3 curr_reservoir_contribution = canonical_reservoir_contribution;
	float m_c = 1.0;
	const float canonical_in_canonical_pdf = calc_target_pdf(canonical_reservoir_contribution);
	uint num_valid_samples = 0;

	// Temporal reuse
	vec3 pos =
		get_hitdata_pos_only(gbuffer.barycentrics, gbuffer.primitive_instance_id.y, gbuffer.primitive_instance_id.x);
	vec4 prev_pos = ubo.prev_view * vec4(pos, 1);
	prev_pos /= prev_pos.z;
	prev_pos = -ubo.prev_projection * prev_pos;
	ivec2 prev_coords = ivec2(0.5 * (1 + prev_pos.xy) * gl_LaunchSizeEXT.xy);
	if (pc_ray.total_frame_num > 0 &&
		!(any(lessThan(prev_coords, ivec2(0))) || any(greaterThanEqual(prev_coords, gl_LaunchSizeEXT.xy)))) {
		num_valid_samples++;
		const uint prev_pixel_idx = prev_coords.x * pc_ray.size_y + prev_coords.y;
		const ReconnectionData data = reconnection_data[(num_spatial_samples + 1) * pixel_idx + 0];
		Reservoir temp_reservoir = in_reservoirs[prev_pixel_idx];
		bool accepted = process_reservoir(reservoir, m_c, canonical_reservoir, temp_reservoir, data, prev_pixel_idx,
										  canonical_in_canonical_pdf, curr_reservoir_contribution);
	}

	bool canonical = combine_reservoir(seed, reservoir, canonical_reservoir, canonical_in_canonical_pdf * m_c);
	if (canonical) {
		curr_reservoir_contribution = canonical_reservoir_contribution;
	}
	const float target_pdf = calc_target_pdf(curr_reservoir_contribution);
	reservoir.W = target_pdf == 0.0 ? 0.0 : reservoir.w_sum / (target_pdf * (1 + num_valid_samples));

	vec3 final_contribution = prefix_contributions.d[pixel_idx];
	if (reservoir.W > 0) {
		final_contribution += curr_reservoir_contribution * reservoir.W;
	}
	reservoir.data.reservoir_contribution = curr_reservoir_contribution;

	out_reservoirs[pixel_idx] = reservoir;
}