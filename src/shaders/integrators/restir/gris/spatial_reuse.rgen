#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_debug_printf : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_EXT_buffer_reference2 : require
#extension GL_EXT_shader_atomic_float : require

#include "gris_commons.glsl"
GrisReservoir reservoirs = GrisReservoir(scene_desc.gris_reservoir_addr);
GrisGBuffer g_buffers = GrisGBuffer(scene_desc.gris_gbuffer_addr);

ivec2 get_neighbor_offset(inout uvec4 seed) {
	const float randa = rand(seed) * 2 * PI;
	const float randr = sqrt(rand(seed)) * pc_ray.max_spatial_radius;
	return ivec2(floor(cos(randa) * randr), floor(sin(randa) * randr));
}

vec3 uniform_sample_light_with_visibility_override(inout uvec4 seed, const Material mat, vec3 pos, const bool side,
												   const vec3 n_s, const vec3 wo, bool visible_light) {
	vec3 res = vec3(0);
	// Sample light
	vec3 wi;
	float wi_len;
	float pdf_light_w;
	float pdf_light_a;
	LightRecord record;
	float cos_from_light;
	const vec3 Le =
		sample_light_Li(seed, pos, pc_ray.num_lights, pdf_light_w, wi, wi_len, pdf_light_a, cos_from_light, record);
	const vec3 p = offset_ray2(pos, n_s);
	float bsdf_pdf;
	float cos_x = dot(n_s, wi);
	vec3 f = eval_bsdf(n_s, wo, mat, 1, side, wi, bsdf_pdf, cos_x);
	if (visible_light && pdf_light_w > 0) {
		const float mis_weight = is_light_delta(record.flags) ? 1 : 1 / (1 + bsdf_pdf / pdf_light_w);
		res += mis_weight * f * abs(cos_x) * Le / pdf_light_w;
	}
	if (get_light_type(record.flags) == LIGHT_AREA) {
		// Sample BSDF
		f = sample_bsdf(n_s, wo, mat, 1, side, wi, bsdf_pdf, cos_x, seed);
		if (bsdf_pdf != 0) {
			traceRayEXT(tlas, flags, 0xFF, 0, 0, 0, p, tmin, wi, tmax, 0);
			if (payload.material_idx == record.material_idx && payload.triangle_idx == record.triangle_idx) {
				const float wi_len = length(payload.pos - pos);
				const float g = abs(dot(payload.n_s, -wi)) / (wi_len * wi_len);
				const float mis_weight = 1. / (1 + pdf_light_a / (g * bsdf_pdf));
				res += f * mis_weight * abs(cos_x) * Le / bsdf_pdf;
			}
		}
	}
	return res;
}

bool connect_vertex(in GBuffer gbuffer, in GrisData data, vec3 direction, uvec4 reservoir_seed,
					out vec3 reservoir_contribution) {
	bool connected = false;

	if (data.rc_mat_id == -1) {
		return false;
	}

	Material hit_mat = load_material(gbuffer.material_idx, gbuffer.uv);
	vec3 pos = gbuffer.pos;
	vec3 prefix_throughput = vec3(1);
	reservoir_contribution = vec3(0);

	uint prefix_depth = 0;
	vec3 n_s = gbuffer.n_s;
	bool side = true;
	vec3 n_g = gbuffer.n_g;
	vec3 wo = -direction;
	bool specular = false;

	if (dot(n_g, wo) < 0.) n_g = -n_g;
	if (dot(n_g, n_s) < 0) {
		n_s = -n_s;
		side = false;
	}
	vec3 origin = offset_ray(gbuffer.pos, n_g);
	while (true) {
		if ((prefix_depth + data.postfix_length) >= pc_ray.max_depth) {
			return false;
		}

		vec3 wi = data.rc_pos - pos;
		float wi_len = length(wi);
		wi /= wi_len;

		if (prefix_depth > 0 && (hit_mat.bsdf_props & BSDF_SPECULAR) == 0) {
			const float light_pick_pdf = 1. / pc_ray.light_triangle_count;
			vec3 contrib = prefix_throughput *
						   uniform_sample_light(reservoir_seed, hit_mat, payload.pos, side, n_s, wo) / light_pick_pdf;
			reservoir_contribution += contrib;
		}

		bool connectable = is_rough(hit_mat) && wi_len > 0.01 * pc_ray.scene_extent;
		connectable = connectable && prefix_depth >= (data.rc_depth - 1);
		if (connectable) {
			vec3 p = offset_ray2(pos, n_s);
			any_hit_payload.hit = 1;
			traceRayEXT(tlas, gl_RayFlagsTerminateOnFirstHitEXT | gl_RayFlagsSkipClosestHitShaderEXT, 0xFF, 1, 0, 1, p,
						0, wi, wi_len - EPS, 1);
			connected = any_hit_payload.hit == 0;
		}

		if (connected) {
			// Update the prefix throughput
			float bsdf_pdf;
			float cos_x = dot(n_s, wi);
			vec3 f = eval_bsdf(n_s, wo, hit_mat, 1, side, wi, bsdf_pdf, cos_x);
			if (bsdf_pdf == 0) {
				return false;
			}

			prefix_throughput *= f * abs(cos_x) / bsdf_pdf;
			// Compute lighting on the connected vertex
			const Material rc_mat = load_material(data.rc_mat_id, data.rc_uv);
			if (is_diffuse(rc_mat)) {
				reservoir_contribution += prefix_throughput * data.rc_nee_L;

			} else {
				const float light_pick_pdf = 1. / pc_ray.light_triangle_count;
				reservoir_contribution +=
					prefix_throughput *
					uniform_sample_light_with_visibility_override(data.rc_seed, rc_mat, data.rc_pos, data.rc_side == 1,
																  data.rc_ns, -wi, data.rc_nee_visible == 1) /
					light_pick_pdf;
			}

			// Evaluate the contribution for the incoming radiance
			cos_x = dot(data.rc_ns, data.rc_wi);
			const vec3 rc_f = eval_bsdf(data.rc_ns, -wi, rc_mat, 1, data.rc_side == 1, data.rc_wi, bsdf_pdf, cos_x);

			reservoir_contribution += prefix_throughput * rc_f * abs(cos_x) * data.rc_postfix_L / bsdf_pdf;
			return true;
		}

		float pdf, cos_theta;
		const vec3 f =
			sample_bsdf(n_s, wo, hit_mat, 1 /*radiance=cam*/, side, direction, pdf, cos_theta, reservoir_seed);
		if (pdf == 0) {
			return false;
		}

		prefix_throughput *= f * abs(cos_theta) / pdf;
		specular = (hit_mat.bsdf_props & BSDF_SPECULAR) != 0;

		traceRayEXT(tlas, flags, 0xFF, 0, 0, 0, origin, tmin, direction, tmax, 0);
		const bool found_isect = payload.material_idx != -1;

		if (!found_isect) {
			return false;
		}
		hit_mat = load_material(payload.material_idx, payload.uv);

		if (specular) {
			reservoir_contribution += prefix_throughput * hit_mat.emissive_factor;
		}

		wo = -direction;
		n_s = payload.n_s;
		side = true;
		n_g = payload.n_g;
		if (dot(payload.n_g, wo) < 0.) n_g = -n_g;
		if (dot(n_g, payload.n_s) < 0) {
			n_s = -n_s;
			side = false;
		}

		origin = offset_ray(payload.pos, n_g);
		pos = payload.pos;

		prefix_depth++;
	}

	return connected;
}

void main() {
	const vec2 pixel = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
	const vec2 in_uv = pixel / vec2(gl_LaunchSizeEXT.xy);
	vec2 d = in_uv * 2.0 - 1.0;
	vec3 direction = vec3(sample_camera(d));
	GBuffer gbuffer = g_buffers.d[pixel_idx];

	GrisData data = reservoirs.d[pixel_idx].gris_data;

	if (gbuffer.material_idx == -1) {
		return;
	}

	vec3 reservoir_contribution = vec3(0);
	bool connected = connect_vertex(gbuffer, data, direction, data.init_seed, reservoir_contribution);
	if (connected) {
		float target_pdf = luminance(reservoir_contribution);
		if (isnan(target_pdf)) {
			return;
		}
		reservoirs.d[pixel_idx].gris_data.F = reservoir_contribution;
	}

#if 0
	for (uint i = 0; i < pc_ray.num_spatial_samples; i++) {
	    ivec2 rand_offset = get_neighbor_offset(seed);
		const ivec2 coords = clamp(ivec2(gl_LaunchIDEXT.xy) + rand_offset, ivec2(0), ivec2(gl_LaunchSizeEXT.xy) - 1);
	    const uint coords_idx = coords.x * pc_ray.size_y + coords.y;
	}
#endif
}