#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_debug_printf : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_EXT_buffer_reference2 : require
#extension GL_EXT_shader_atomic_float : require

#define SCENE_TEX_IDX 8
#include "gris_commons.glsl"
GrisDirectLighting direct_lighting_buffer = GrisDirectLighting(scene_desc.gris_direct_lighting_addr);
PrefixContributions prefix_contributions = PrefixContributions(scene_desc.prefix_contributions_addr);
layout(binding = 4, std430) readonly buffer PathReconnections { ReconnectionData reconnection_data[]; };
layout(binding = 5, std430) readonly buffer InReservoirs { Reservoir in_reservoirs[]; };
layout(binding = 6, std430) writeonly buffer OutReservoirs { Reservoir out_reservoirs[]; };
layout(binding = 7, std430) readonly buffer CurrGBuffer { GBuffer curr_gbuffers[]; };
uvec4 seed = init_rng(gl_LaunchIDEXT.xy, gl_LaunchSizeEXT.xy, pc.random_num);
uvec4 seed2 = init_rng(gl_LaunchIDEXT.xy, gl_LaunchSizeEXT.xy, 0);

// TODO: Maybe use an external buffer to supply spatial offsets?

bool process_reservoir(inout Reservoir reservoir, inout float m_c, in Reservoir canonical_reservoir,
					   in Reservoir source_reservoir, in ReconnectionData data, ivec2 neighbor_coords,
					   float canonical_in_canonical_pdf, inout vec3 curr_reservoir_contribution) {
	source_reservoir.M = min(source_reservoir.M, 20);

	float neighbor_in_neighbor_pdf = calc_target_pdf(source_reservoir.data.reservoir_contribution);
	float neighbor_in_canonical_pdf = calc_target_pdf(data.reservoir_contribution) * data.jacobian;

	float m_i_num = source_reservoir.M * neighbor_in_neighbor_pdf;
	float m_i_denom = m_i_num + canonical_reservoir.M * neighbor_in_canonical_pdf / pc.num_spatial_samples;
	float m_i = m_i_denom == 0 ? 0 : m_i_num / m_i_denom;

	// if (m_i <= 0.0 || m_i >= 1.0) {
	// 	reservoir.M += source_reservoir.M;
	// 	m_c += 1.0;
	// 	return false;
	// }

	m_c += 1.0;
	float m_c_num = source_reservoir.M * data.target_pdf_in_neighbor;
	float m_c_denom = m_c_num + canonical_reservoir.M * canonical_in_canonical_pdf / pc.num_spatial_samples;
	float m_c_val = m_c_denom == 0.0 ? 0.0 : m_c_num / m_c_denom;
	if (m_c_val > 0) {
		m_c -= m_c_val;
	}
	// if (m_c_val <= 0.0) {
	// 	reservoir.M += source_reservoir.M;
	// 	return false;
	// }

	ASSERT1(m_c_val > -1e-3 && m_c_val <= 1.001, "m_c_val <= 1.0 : %f\n", m_c_val);
	ASSERT1(m_i > -1e-3 && m_i <= 1.001, "m_i <= 1.0 : %f\n", m_i);

	bool accepted = combine_reservoir(seed, reservoir, source_reservoir, calc_target_pdf(data.reservoir_contribution),
									  m_i, data.jacobian);
	if (accepted) {
		curr_reservoir_contribution = data.reservoir_contribution;
	}
	return true;
}

void main() {
	GBuffer gbuffer = curr_gbuffers[pixel_idx];
	if (!gbuffer_data_valid(gbuffer)) {
		imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(0));
		return;
	}

	Reservoir reservoir;
	init_reservoir(reservoir);

	Reservoir canonical_reservoir = in_reservoirs[pixel_idx];

	vec3 canonical_reservoir_contribution = canonical_reservoir.data.reservoir_contribution;
	uint num_spatial_samples = pc.enable_spatial_reuse == 1 ? pc.num_spatial_samples : 0;
	float m_c = 1.0;
	float canonical_in_canonical_pdf = calc_target_pdf(canonical_reservoir_contribution);
	uint num_valid_samples = 0;
	vec3 curr_reservoir_contribution = vec3(0);

	HitData canonical_gbuffer =
		get_hitdata(gbuffer.barycentrics, gbuffer.primitive_instance_id.y, gbuffer.primitive_instance_id.x);
	uint seed_helper = pc.random_num;
	uvec4 neighbor_seed = seed2;
	for (int i = 0; i < num_spatial_samples; i++) {
		ivec2 neighbor_coords = ivec2(gl_LaunchIDEXT.xy) + get_neighbor_offset(neighbor_seed);
		uint neighbor_pixel_idx = neighbor_coords.x * pc.size_y + neighbor_coords.y;
		if (any(lessThan(neighbor_coords, ivec2(0))) || any(greaterThanEqual(neighbor_coords, gl_LaunchSizeEXT.xy))) {
			continue;
		}
		GBuffer neighbor_gbuffer = curr_gbuffers[neighbor_pixel_idx];
		if (!gbuffer_data_valid(neighbor_gbuffer)) {
			continue;
		}
		num_valid_samples++;
		Reservoir neighbor_reservoir = in_reservoirs[neighbor_pixel_idx];
#if 1
		ReconnectionData data = reconnection_data[(num_spatial_samples + 1) * pixel_idx + i + 1];
#else
		ReconnectionData data;
		data.reservoir_contribution = vec3(0);
		data.jacobian = 0;
		data.target_pdf_in_neighbor = 0;
		HitData unpacked_neighbor_gbuffer =
			get_hitdata(neighbor_gbuffer.barycentrics, neighbor_gbuffer.primitive_instance_id.y,
						neighbor_gbuffer.primitive_instance_id.x);
		retrace_paths(canonical_gbuffer, unpacked_neighbor_gbuffer, neighbor_reservoir.data, gl_LaunchIDEXT.xy,
					  uvec2(neighbor_coords), seed_helper, data.jacobian, data.reservoir_contribution);
		retrace_paths_and_evaluate(unpacked_neighbor_gbuffer, canonical_gbuffer, canonical_reservoir.data,
								   uvec2(neighbor_coords), gl_LaunchIDEXT.xy, seed_helper, data.target_pdf_in_neighbor);
#endif
		process_reservoir(reservoir, m_c, canonical_reservoir, neighbor_reservoir, data, neighbor_coords,
						  canonical_in_canonical_pdf, curr_reservoir_contribution);
	}

	bool canonical = combine_reservoir(seed, reservoir, canonical_reservoir, canonical_in_canonical_pdf, m_c, 1.0);
	if (canonical) {
		curr_reservoir_contribution = canonical_reservoir_contribution;
	}
	const float target_pdf = calc_target_pdf(curr_reservoir_contribution);
	calc_reservoir_W_with_mis(reservoir);
	reservoir.W /= (1.0 + num_valid_samples);

	vec3 final_contribution = prefix_contributions.d[pixel_idx];
	if (reservoir.W > 0) {
		final_contribution += curr_reservoir_contribution * reservoir.W;
	}

	vec3 col = vec3(0);
	if (pc.direct_lighting == 1) {
		col += direct_lighting_buffer.d[pixel_idx];
	}
	if (pc.show_reconnection_radiance == 1 && reservoir.W > 0) {
		col += reservoir.data.rc_Li;
	} else {
		col += final_contribution;
	}

	ivec2 neighbor_coords = ivec2(gl_LaunchIDEXT.xy);
	if (pc.enable_accumulation == 0 || pc.frame_num == 0) {
		imageStore(image, ivec2(neighbor_coords), vec4(col, 1.f));
	} else {
		float w = 1. / float(pc.frame_num + 1);
		vec3 old_col = imageLoad(image, ivec2(neighbor_coords)).xyz;
		imageStore(image, ivec2(neighbor_coords), vec4(mix(old_col, col, w), 1.f));
	}
}