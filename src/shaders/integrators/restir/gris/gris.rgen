#version 460
#extension GL_EXT_ray_tracing : require
#extension GL_EXT_nonuniform_qualifier : enable
#extension GL_EXT_scalar_block_layout : enable
#extension GL_GOOGLE_include_directive : enable
#extension GL_EXT_debug_printf : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int64 : require
#extension GL_EXT_buffer_reference2 : require
#extension GL_EXT_shader_atomic_float : require
#define SCENE_TEX_IDX 6
#include "gris_commons.glsl"

GrisDirectLighting direct_lighting_buffer = GrisDirectLighting(scene_desc.gris_direct_lighting_addr);
PrefixContributions prefix_contributions = PrefixContributions(scene_desc.prefix_contributions_addr);

layout(binding = 4, std430) writeonly buffer OutReservoirs { Reservoir out_reservoirs[]; };
layout(binding = 5, std430) writeonly buffer OutGBuffer { GBuffer out_gbuffers[]; };

uvec4 seed = init_rng(gl_LaunchIDEXT.xy, gl_LaunchSizeEXT.xy, pc_ray.total_frame_num ^ pc_ray.random_num);

void main() {
	const vec2 pixel = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
	const vec2 in_uv = pixel / vec2(gl_LaunchSizeEXT.xy);
	vec2 d = in_uv * 2.0 - 1.0;
	vec4 origin = ubo.inv_view * vec4(0, 0, 0, 1);
	vec4 target = ubo.inv_projection * vec4(d.x, d.y, 1, 1);
	vec3 direction = vec3(sample_camera(d));
	vec3 col = vec3(0);

	GBuffer gbuffer_out;
	init_gbuffer(gbuffer_out);

	bool specular = false;
	vec3 partial_throughput = vec3(1);
	float joint_pdf = 1.0;
	vec3 direct_lighting = vec3(0);
	float bsdf_pdf;
	int depth;

	// GRIS variables
	bool connectable_vertex_found = false;
	bool prev_rough = false;
	bool curr_rough = false;
	uint init_seed;
	Reservoir reservoir;
	GrisData reconnection_data;
	init_reservoir(reservoir);
	init_data(reconnection_data);
	init_data(reservoir.data);
	vec3 postfix_L = vec3(0);
	vec3 postfix_throughput = vec3(1);
	float prefix_pdf_times_rc_pdf;
	int postfix_depth = 0;
	int prefix_depth;
	vec3 prefix_col = vec3(0);

	// Common reconnection data
	vec2 rc_barycentrics;
	uvec2 rc_primitive_instance_id;
	uint rc_coords = gl_LaunchIDEXT.x * gl_LaunchSizeEXT.y + gl_LaunchIDEXT.y;
	float g;

	bool valid_reservoir = false;
	for (depth = 0;; depth++) {
		traceRayEXT(tlas, flags, 0xFF, 0, 0, 0, origin.xyz, tmin, direction, tmax, 0);
		const bool found_isect = payload.instance_idx != -1;
		if (depth >= pc_ray.max_depth - 1) {
			break;
		}
		if (!found_isect) {
			vec3 atmosphere_L = shade_atmosphere(pc_ray.dir_light_idx, pc_ray.sky_col, origin.xyz, direction, tmax);
			vec3 contribution = partial_throughput * atmosphere_L / joint_pdf;
			if (depth > 0) {
				col += contribution;
			} else {
				direct_lighting += contribution;
			}
			if (connectable_vertex_found) {
				postfix_L += postfix_throughput * atmosphere_L;
			}
			break;
		}

		float triangle_area = 0;
		HitData gbuffer = get_hitdata(payload.attribs, payload.instance_idx, payload.triangle_idx, triangle_area);
		const Material hit_mat = load_material(gbuffer.material_idx, gbuffer.uv);

		prev_rough = curr_rough;
		curr_rough = is_rough(hit_mat);
		// TODO: Check hit distance
		bool connectable = !connectable_vertex_found && ((prev_rough && curr_rough));

		const vec3 wo = -direction;
		vec3 n_s = gbuffer.n_s;
		vec3 n_g = gbuffer.n_g;

		bool side = face_forward(n_s, n_g, wo);

		// Fill in GBuffer
		if (depth == 0) {
			direct_lighting += partial_throughput * hit_mat.emissive_factor / joint_pdf;
			gbuffer_out.barycentrics = payload.attribs;
			gbuffer_out.primitive_instance_id = uvec2(payload.triangle_idx, payload.instance_idx);
		} else if (depth > 0) {
			float mis_weight = 1.0;
			float pdf_light_w;
			if (!specular) {
				float g = abs(dot(-direction, n_s)) / (payload.dist * payload.dist);
				float pdf_light_w = 1.0 / (g * triangle_area);
				mis_weight = 1.0 / (1 + pdf_light_w / bsdf_pdf);
			}
			vec3 partial_contrib = mis_weight * hit_mat.emissive_factor;
			if (depth == 1) {
				direct_lighting += partial_contrib * partial_throughput / joint_pdf;
			} else {
				col += partial_contrib * partial_throughput / joint_pdf;
			}
			if (connectable_vertex_found) {
				postfix_L += partial_contrib * postfix_throughput;
			}
		}

		origin.xyz = offset_ray(gbuffer.pos, n_g);

		vec3 rc_Li = vec3(0);
		specular = (hit_mat.bsdf_props & BSDF_SPECULAR) != 0;
		// NEE
		if (!specular) {
			vec3 wi;
			uint rc_seed = seed.w;
			uvec4 debug_seed = seed;
			vec3 Li = do_nee(seed, gbuffer, hit_mat, side, n_s, wo, wi, true);
			vec3 partial_contrib = partial_throughput * Li;
			vec3 contribution = partial_contrib / joint_pdf;

			// Stream the NEE path at the reconnection vertex
			if (connectable) {
				g = (abs(dot(n_s, wo))) / (dot(payload.dist, payload.dist));
				rc_barycentrics = payload.attribs;
				rc_primitive_instance_id = uvec2(payload.triangle_idx, payload.instance_idx);
				reconnection_data.rc_seed = rc_seed;
				reconnection_data.rc_wi = wi;
				reconnection_data.rc_Li = Li;
				reconnection_data.reservoir_contribution = partial_throughput * Li;
				// reconnection_data.reservoir_contribution = Li;
				reservoir.data.debug_seed = debug_seed;
				reconnection_data.path_flags = set_path_flags(depth, 1, true);
				valid_reservoir =
					valid_reservoir ||
					stream_reservoir(seed, reservoir, reconnection_data,
									 calc_target_pdf(reconnection_data.reservoir_contribution), 1.0 / joint_pdf);
				prefix_col = col;
				if (!valid_reservoir) {
					ASSERT(reservoir.data.path_flags == 0);
				} else {
					ASSERT(reservoir.data.path_flags != 0);
				}
			} else if (connectable_vertex_found) {
				postfix_L += Li * postfix_throughput;
			}

			if (depth > 0) {
				col += contribution;
			} else {
				direct_lighting += contribution;
			}
		}

		// Record initial seed after direct light sampling
		if (depth == 0) {
			init_seed = seed.w;
		}

		// Sample direction & update partial_throughput
		float cos_theta;
		const vec3 f = sample_bsdf(n_s, wo, hit_mat, 1 /*radiance=cam*/, side, direction, bsdf_pdf, cos_theta, seed);
		if (bsdf_pdf == 0) {
			break;
		}
		vec3 f_cos = f * abs(cos_theta);
		partial_throughput *= f_cos;
		joint_pdf *= bsdf_pdf;
		if (connectable_vertex_found) {
			postfix_throughput *= f_cos / bsdf_pdf;
			postfix_depth++;
		}

		if (connectable) {
			connectable_vertex_found = true;
			prefix_depth = depth;
			// Prepare reconnection data for the non-NEE path
			reconnection_data.rc_wi = direction;
			// Seed is not relevant here
			reconnection_data.rc_seed = -1;
			// Partially fill in the contribution data
			reconnection_data.reservoir_contribution = partial_throughput;
			prefix_pdf_times_rc_pdf = joint_pdf;
		}

		if (pc_ray.enable_rr == 1) {
			float rr_scale = 1.0;
			if (hit_mat.bsdf_type == BSDF_GLASS) {
				rr_scale *= side ? 1. / hit_mat.ior : hit_mat.ior;
			}
			if (depth > RR_MIN_DEPTH) {
				float rr_prob = min(0.95f, luminance(partial_throughput / joint_pdf) * rr_scale);
				if (rr_prob == 0 || rr_prob < rand(seed))
					break;
				else
					partial_throughput /= rr_prob;
			}
		}
	}

	if (isnan(luminance(col))) {
		return;
	}
	// Stream the non-NEE path
	if (pc_ray.enable_gris == 1 && connectable_vertex_found) {
		reconnection_data.rc_Li = postfix_L;
		reconnection_data.reservoir_contribution = reconnection_data.reservoir_contribution * postfix_L;
		reconnection_data.path_flags = set_path_flags(prefix_depth, postfix_depth, false);
		ASSERT(reconnection_data.path_flags != 0);
		bool merged =
			stream_reservoir(seed, reservoir, reconnection_data,
							 calc_target_pdf(reconnection_data.reservoir_contribution), 1.0 / prefix_pdf_times_rc_pdf);
		valid_reservoir = valid_reservoir || merged;

		if (valid_reservoir) {
			reservoir.data.rc_barycentrics = rc_barycentrics;
			reservoir.data.rc_primitive_instance_id = rc_primitive_instance_id;
			reservoir.data.rc_coords = rc_coords;
			reservoir.data.rc_g = g;


			bool is_nee;
			uint rc_prefix_length;
			uint rc_postfix_length;
			unpack_path_flags(reservoir.data.path_flags, is_nee, rc_prefix_length, rc_postfix_length);
			if (merged) {
				ASSERT(!is_nee);
				ASSERT(reservoir.data.rc_seed == -1);
			} else {
				ASSERT(is_nee);
				ASSERT(reservoir.data.rc_seed != -1);
			}
			ASSERT(reservoir.data.path_flags != 0);
			ASSERT(reservoir_data_valid(reservoir.data));
		}
	}
	if (pc_ray.direct_lighting == 1) {
		col += direct_lighting;
	}


	if(pc_ray.enable_gris == 1)  {
		// The samples are additive, therefore set M to 1
		reservoir.M = 1;
		calc_reservoir_W(reservoir);
		col = prefix_col + reservoir.data.reservoir_contribution * reservoir.W;

	}

	if (pc_ray.enable_gris == 0) {
		if (pc_ray.frame_num > 0) {
			float w = 1. / float(pc_ray.frame_num + 1);
			vec3 old_col = imageLoad(image, ivec2(gl_LaunchIDEXT.xy)).xyz;
			// imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(col, 1.f));
			imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(mix(old_col, col, w), 1.f));
		} else {
			imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(col, 1.f));
		}
	} else {
		if (pc_ray.direct_lighting == 1) {
			direct_lighting_buffer.d[pixel_idx] = direct_lighting;
		}
		if (!valid_reservoir) {
			ASSERT(!reservoir_data_valid(reservoir.data));
			ASSERT(reservoir.data.path_flags == 0);
		}

		out_reservoirs[pixel_idx] = reservoir;
		out_gbuffers[pixel_idx] = gbuffer_out;
	}
}